plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.7'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.asciidoctor.jvm.convert' version '4.0.5'
	id 'com.epages.restdocs-api-spec' version '0.19.2'
}

group = 'app.mockly'
version = '0.0.1-SNAPSHOT'
description = 'Mockly: AI based Mock Interview platform'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
    asciidoctorExt
}

repositories {
	mavenCentral()
}

ext {
	set('snippetsDir', file("build/generated-snippets"))
	set('springAiVersion', "1.0.3")
}

openapi3 {
	server = 'http://localhost:8080'
	title = 'Mockly API'
	description = 'AI 기반 모의 면접 플랫폼 API'
	outputDirectory = "src/main/resources/static"
	version = '0.0.1-SNAPSHOT'
	format = 'yaml'
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
//	implementation 'org.springframework.ai:spring-ai-advisors-vector-store'
//	implementation 'org.springframework.ai:spring-ai-starter-model-chat-memory-repository-jdbc'
//	implementation 'org.springframework.ai:spring-ai-starter-model-openai'
//	implementation 'org.springframework.ai:spring-ai-starter-vector-store-pgvector'
    implementation 'com.fasterxml.uuid:java-uuid-generator:4.3.0'

	implementation 'io.portone:server-sdk:0.22.0'

	implementation("com.google.api-client:google-api-client:2.7.0")
	implementation("io.jsonwebtoken:jjwt-api:0.12.6")
	runtimeOnly("io.jsonwebtoken:jjwt-impl:0.12.6")
	runtimeOnly("io.jsonwebtoken:jjwt-jackson:0.12.6")

	// Swagger UI
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'

	compileOnly 'org.projectlombok:lombok'
    developmentOnly 'me.paulschwarz:spring-dotenv:4.0.0'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.projectlombok:lombok'

    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
	testImplementation 'com.epages:restdocs-api-spec-mockmvc:0.19.2'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.ai:spring-ai-bom:${springAiVersion}"
	}
}

tasks.named('test') {
	outputs.dir snippetsDir
	useJUnitPlatform()
}

tasks.named('asciidoctor') {
	inputs.dir snippetsDir
    configurations 'asciidoctorExt'
	dependsOn test
}

tasks.register('bearerAuthentication') {
	mustRunAfter tasks.named('openapi3')
	doLast {
		def apiSpecFile = file('src/main/resources/static/openapi3.yaml')
		def contentToAdd =
				"  securitySchemes:\n" +
				"    bearerAuth:\n" +
				"      type: http\n" +
				"      scheme: bearer\n" +
				"      bearerFormat: JWT\n" +
				"\n" +
				"security:\n" +
				"  - bearerAuth: []"
		def existingContent = apiSpecFile.text
		def updatedContent = existingContent + contentToAdd
		apiSpecFile.withWriter('UTF-8') {
			writer -> writer.write(updatedContent)
		}
	}
}

tasks.named('build') {
	dependsOn tasks.named('openapi3')
	dependsOn tasks.named('bearerAuthentication')
}

tasks.named('bootJar') {
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}") {
        into 'static/docs'
    }
}
